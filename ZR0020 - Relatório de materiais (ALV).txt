*&---------------------------------------------------------------------*
*& Report ZR0020
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
REPORT ZR0020 NO STANDARD PAGE HEADING.

*Grupo de tipos.
TYPE-POOLS SLIS.

*Tabela transparente.
TABLES: ZT0005.

*Tabelas internas.
DATA: TI_ZT0005   TYPE TABLE OF ZT0005,
      TI_ZT0001   TYPE TABLE OF ZT0001,
      TI_SAIDA    TYPE TABLE OF ZS0001,
      TI_FIELDCAT TYPE SLIS_T_FIELDCAT_ALV,
      TI_SORT     TYPE SLIS_T_SORTINFO_ALV,
      TI_HEADER   TYPE SLIS_T_LISTHEADER.


*Work-area.
DATA: WA_ZT0005   TYPE ZT0005,
      WA_ZT0001   TYPE ZT0001,
      WA_SAIDA    TYPE ZS0001,
      WA_FIELDCAT TYPE SLIS_FIELDCAT_ALV,
      WA_SORT     TYPE SLIS_SORTINFO_ALV,
      WA_LAYOUT   TYPE SLIS_LAYOUT_ALV,
      WA_HEADER   TYPE SLIS_LISTHEADER,
      WA_VARIANT  TYPE DISVARIANT.

*Tela de seleção
SELECTION-SCREEN BEGIN OF BLOCK B1 WITH FRAME TITLE TEXT-001.

SELECT-OPTIONS: S_TPMAT FOR ZT0005-TPMAT,
                S_MATER FOR ZT0005-MATER.

SELECTION-SCREEN END OF BLOCK B1.

SELECTION-SCREEN BEGIN OF BLOCK B2 WITH FRAME TITLE TEXT-002.

PARAMETERS: P_VARIAN TYPE SLIS_VARI.

SELECTION-SCREEN END OF BLOCK B2.

AT SELECTION-SCREEN ON VALUE-REQUEST FOR P_VARIAN.
  PERFORM F_VARIANT_F4 CHANGING P_VARIAN.

START-OF-SELECTION.

  PERFORM F_SELECIONAR_DADOS.
  PERFORM F_MONTAR_TI_SAIDA.
  PERFORM F_MONTAR_ALV.

*&---------------------------------------------------------------------*
*& Form F_SELECIONAR_DADOS
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM F_SELECIONAR_DADOS .

  SELECT * FROM ZT0005 INTO TABLE TI_ZT0005
    WHERE TPMAT IN S_TPMAT
      AND MATER IN S_MATER.

  IF SY-SUBRC IS INITIAL.

    SELECT * FROM ZT0001 INTO TABLE TI_ZT0001
      FOR ALL ENTRIES IN TI_ZT0005
      WHERE TPMAT = TI_ZT0005-TPMAT.
  ELSE.
    MESSAGE TEXT-003 TYPE 'I'. "Não foi encontrado nenhum registro com esses parâmetros.
    STOP.

  ENDIF.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form F_MONTAR_TABELA_SAIDA
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM F_MONTAR_TI_SAIDA .

  LOOP AT TI_ZT0005 INTO WA_ZT0005.

    CLEAR WA_SAIDA.

    WA_SAIDA-MATER  = WA_ZT0005-MATER.
    WA_SAIDA-DENOM  = WA_ZT0005-DENOM.
    WA_SAIDA-BRGEW  = WA_ZT0005-BRGEW.
    WA_SAIDA-NTGEW  = WA_ZT0005-NTGEW.
    WA_SAIDA-GEWEI  = WA_ZT0005-GEWEI.
    WA_SAIDA-STATUS = WA_ZT0005-STATUS.
    WA_SAIDA-TPMAT  = WA_ZT0005-TPMAT.


    READ TABLE TI_ZT0001 INTO WA_ZT0001 WITH KEY TPMAT = WA_ZT0005-TPMAT.
    IF SY-SUBRC IS INITIAL.
      WA_SAIDA-DENOM_TP = WA_ZT0001-DENOM.
    ENDIF.

    APPEND WA_SAIDA TO TI_SAIDA.


  ENDLOOP.


ENDFORM.
*&---------------------------------------------------------------------*
*& Form F_MONTAR_ALV
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM F_MONTAR_ALV .

  PERFORM F_DEFINIR_FIELDCAT.
  PERFORM F_ORDENA. "Form para colocar a tabela ordenada por coluna e decrescente.
  PERFORM F_LAYOUT. "Form para estabelecer a cor das linhas e dimencionar a largura do campo.
  PERFORM F_CABECALHO."Cabeçalho criado para ter o titulo do programa, data, hora e logo tipo do SAP
  PERFORM F_IMPRIMIR_ALV.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form F_DEFINIR_FIELDCAT
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM F_DEFINIR_FIELDCAT . "Serve para definir as caracteristicas das colunas do ALV.

  CALL FUNCTION 'REUSE_ALV_FIELDCATALOG_MERGE'
    EXPORTING
      I_PROGRAM_NAME         = SY-REPID "Variável do sistema que armazena o nome do programa.
      I_INTERNAL_TABNAME     = 'TI_SAIDA' "Onde está a carga de dados para impressão.
      I_STRUCTURE_NAME       = 'ZS0001' "Estrutura criada para impressão do relatório.
    CHANGING
      CT_FIELDCAT            = TI_FIELDCAT "Tabela interna com os campos do Catálogo (FieldCat).
    EXCEPTIONS
      INCONSISTENT_INTERFACE = 1
      PROGRAM_ERROR          = 2
      OTHERS                 = 3.
  IF SY-SUBRC <> 0.
    MESSAGE  TEXT-006 TYPE 'I'. "Erro na definição da fieldcat.
    STOP.
  ELSE.

    LOOP AT TI_FIELDCAT INTO WA_FIELDCAT.

      CASE WA_FIELDCAT-FIELDNAME.

        WHEN 'BRGEW'.
          WA_FIELDCAT-SELTEXT_S = WA_FIELDCAT-SELTEXT_M = WA_FIELDCAT-SELTEXT_L = WA_FIELDCAT-REPTEXT_DDIC = TEXT-004. "Peso bruto.

        WHEN 'NTGEW'.
          WA_FIELDCAT-SELTEXT_S = WA_FIELDCAT-SELTEXT_M = WA_FIELDCAT-SELTEXT_L = WA_FIELDCAT-REPTEXT_DDIC = TEXT-005. "Peso liquído.

        WHEN 'GEWEI'.
          WA_FIELDCAT-SELTEXT_S = WA_FIELDCAT-SELTEXT_M = WA_FIELDCAT-SELTEXT_L = WA_FIELDCAT-REPTEXT_DDIC = TEXT-007. "Peso unitário.

        WHEN 'MATER'.
          WA_FIELDCAT-HOTSPOT = 'X'.

      ENDCASE.

      MODIFY TI_FIELDCAT FROM WA_FIELDCAT INDEX SY-TABIX TRANSPORTING SELTEXT_S SELTEXT_M SELTEXT_L REPTEXT_DDIC HOTSPOT.

    ENDLOOP.
  ENDIF.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form F_ORDENA
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
"Form para colocar a tabela ordenada por coluna e decrescente.
FORM F_ORDENA .

  CLEAR WA_SORT.
  WA_SORT-SPOS =  2.
  WA_SORT-FIELDNAME = 'DENOM'.
  WA_SORT-TABNAME = 'TI_SAIDA'.
  WA_SORT-UP = 'X'.

  APPEND WA_SORT TO TI_SORT.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form F_LAYOUT
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM F_LAYOUT .

  WA_LAYOUT-ZEBRA = 'X'. "parametro para fazer a linha ficar com uma cor e a outra com cor diferente.
  WA_LAYOUT-COLWIDTH_OPTIMIZE = 'X'. "parâmetro para dimencionar o tabalho de campo de acordo com o dado inserido na field.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form F_CABECALHO
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
*Cabeçalho criado para ter o titulo do programa, data, hora e logo tipo do SAP
FORM F_CABECALHO.

  CLEAR WA_HEADER.
  REFRESH TI_HEADER.

  WA_HEADER-TYP  = 'H'.
  WA_HEADER-INFO = TEXT-008. "Relatório de materiais.
  APPEND WA_HEADER TO TI_HEADER.

  WA_HEADER-TYP  = 'S'.
  WA_HEADER-KEY = TEXT-009. "Data.
  WRITE: SY-DATUM TO WA_HEADER-INFO.
  APPEND WA_HEADER TO TI_HEADER.

  WA_HEADER-TYP  = 'S'.
  WA_HEADER-KEY = TEXT-010. "Hora.
  WRITE: SY-UZEIT TO WA_HEADER-INFO.
  APPEND WA_HEADER TO TI_HEADER.


  CALL FUNCTION 'REUSE_ALV_COMMENTARY_WRITE'
    EXPORTING
      IT_LIST_COMMENTARY = TI_HEADER
      I_LOGO             = 'ENJOYSAP_LOGO'
*     I_END_OF_LIST_GRID =
*     I_ALV_FORM         =
    .




ENDFORM.
*&---------------------------------------------------------------------*
*& Form F_IMPRIMIR_ALV
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*& -->  p1        text
*& <--  p2        text
*&---------------------------------------------------------------------*
FORM F_IMPRIMIR_ALV .

  WA_VARIANT-VARIANT = P_VARIAN.


  CALL FUNCTION 'REUSE_ALV_GRID_DISPLAY'
    EXPORTING
*     I_INTERFACE_CHECK       = ' '
*     I_BYPASSING_BUFFER      = ' '
*     I_BUFFER_ACTIVE         = ' '
      I_CALLBACK_PROGRAM      = SY-REPID
*     I_CALLBACK_PF_STATUS_SET          = ' '
      I_CALLBACK_USER_COMMAND = 'USER_COMAND' "Esse form foi criado para quando clicar no campo do material, ser direcionado para o cadastro do material.
      I_CALLBACK_TOP_OF_PAGE  = 'F_CABECALHO' "Cabeçalho criado para ter o titulo do programa, data, hora e logo tipo do SAP
*     I_CALLBACK_HTML_TOP_OF_PAGE       = ' '
*     I_CALLBACK_HTML_END_OF_LIST       = ' '
*     I_STRUCTURE_NAME        =
*     I_BACKGROUND_ID         = ' '
*     I_GRID_TITLE            =
*     I_GRID_SETTINGS         =
      IS_LAYOUT               = WA_LAYOUT
      IT_FIELDCAT             = TI_FIELDCAT
*     IT_EXCLUDING            =
*     IT_SPECIAL_GROUPS       =
      IT_SORT                 = TI_SORT
*     IT_FILTER               =
*     IS_SEL_HIDE             =
*     I_DEFAULT               = 'X'
      I_SAVE                  = 'X'
      IS_VARIANT              = WA_VARIANT
*     IT_EVENTS               =
*     IT_EVENT_EXIT           =
*     IS_PRINT                =
*     IS_REPREP_ID            =
*     I_SCREEN_START_COLUMN   = 0
*     I_SCREEN_START_LINE     = 0
*     I_SCREEN_END_COLUMN     = 0
*     I_SCREEN_END_LINE       = 0
*     I_HTML_HEIGHT_TOP       = 0
*     I_HTML_HEIGHT_END       = 0
*     IT_ALV_GRAPHICS         =
*     IT_HYPERLINK            =
*     IT_ADD_FIELDCAT         =
*     IT_EXCEPT_QINFO         =
*     IR_SALV_FULLSCREEN_ADAPTER        =
* IMPORTING
*     E_EXIT_CAUSED_BY_CALLER =
*     ES_EXIT_CAUSED_BY_USER  =
    TABLES
      T_OUTTAB                = TI_SAIDA
    EXCEPTIONS
      PROGRAM_ERROR           = 1
      OTHERS                  = 2.

ENDFORM.
*&---------------------------------------------------------------------*
*& Form F_VARIAN_F4
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      <-- P_VARIAN
*&---------------------------------------------------------------------*
FORM F_VARIANT_F4  CHANGING P_P_VARIAN. "Form para gravar a formatação do ALV.

  DATA: VL_VARIANT TYPE DISVARIANT.

  VL_VARIANT-REPORT = SY-REPID.

  CALL FUNCTION 'REUSE_ALV_VARIANT_F4'
    EXPORTING
      IS_VARIANT    = VL_VARIANT
*     I_TABNAME_HEADER          =
*     I_TABNAME_ITEM            =
*     IT_DEFAULT_FIELDCAT       =
      I_SAVE        = 'A'
*     I_DISPLAY_VIA_GRID        = ' '
    IMPORTING
*     E_EXIT        =
      ES_VARIANT    = VL_VARIANT
    EXCEPTIONS
      NOT_FOUND     = 1
      PROGRAM_ERROR = 2
      OTHERS        = 3.
  IF SY-SUBRC = 0.
    P_P_VARIAN = VL_VARIANT-VARIANT.
  ENDIF.




ENDFORM.
*Esse form foi criado para quando clicar no campo do material, ser direcionado para o cadastro do material.
FORM USER_COMAND USING R_UCOMM LIKE SY-UCOMM "Variável sy-ucomm armazena o botão que foi clicado.
                       RS_SELFIELD TYPE SLIS_SELFIELD. "RS_SELFIELD está recebendo o valor que eu cliquei.

  DATA: TL_VIMSELLIST TYPE STANDARD TABLE OF VIMSELLIST,
        WL_VIMSELLIST TYPE VIMSELLIST.

  IF RS_SELFIELD-SEL_TAB_FIELD = 'TI_SAIDA-MATER'.

    WL_VIMSELLIST-VIEWFIELD = 'MATER'.
    WL_VIMSELLIST-OPERATOR  = 'EQ'.
    WL_VIMSELLIST-VALUE     = RS_SELFIELD-VALUE.
    APPEND WL_VIMSELLIST TO TL_VIMSELLIST.

    CALL FUNCTION 'VIEW_MAINTENANCE_CALL'
      EXPORTING
        ACTION                       = 'S'
*       CORR_NUMBER                  = '          '
*       GENERATE_MAINT_TOOL_IF_MISSING       = ' '
*       SHOW_SELECTION_POPUP         = '  '
        VIEW_NAME                    = 'ZT0005'
*       NO_WARNING_FOR_CLIENTINDEP   = ' '
*       RFC_DESTINATION_FOR_UPGRADE  = ' '
*       CLIENT_FOR_UPGRADE           = ' '
*       VARIANT_FOR_SELECTION        = ' '
*       COMPLEX_SELCONDS_USED        = ' '
*       CHECK_DDIC_MAINFLAG          = ' '
*       SUPPRESS_WA_POPUP            = ' '
      TABLES
        DBA_SELLIST                  = TL_VIMSELLIST
*       EXCL_CUA_FUNCT               =
      EXCEPTIONS
        CLIENT_REFERENCE             = 1
        FOREIGN_LOCK                 = 2
        INVALID_ACTION               = 3
        NO_CLIENTINDEPENDENT_AUTH    = 4
        NO_DATABASE_FUNCTION         = 5
        NO_EDITOR_FUNCTION           = 6
        NO_SHOW_AUTH                 = 7
        NO_TVDIR_ENTRY               = 8
        NO_UPD_AUTH                  = 9
        ONLY_SHOW_ALLOWED            = 10
        SYSTEM_FAILURE               = 11
        UNKNOWN_FIELD_IN_DBA_SELLIST = 12
        VIEW_NOT_FOUND               = 13
        MAINTENANCE_PROHIBITED       = 14
        OTHERS                       = 15.
    IF SY-SUBRC <> 0.
* Implement suitable error handling here
    ENDIF.


  ENDIF.

ENDFORM.